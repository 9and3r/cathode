apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'android-apt'

dependencies {
  compile parent.libraries.gson

  compile parent.libraries.timber

  compile parent.libraries.dagger
  apt parent.libraries.daggerCompiler
  compile parent.libraries.retrofit
  compile parent.libraries.okhttp
  compile parent.libraries.okhttpUrlConnection

  compile parent.libraries.jodaTime
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion parent.ext.minSdkVersion
    targetSdkVersion parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName
  }

  lintOptions {
    abortOnError false
  }
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  project.tasks.getByName("check").dependsOn checkstyle
}
