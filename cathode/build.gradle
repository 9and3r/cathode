import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'
apply plugin: 'android-apt'

dependencies {
  compile project(':trakt-api')
  compile parent.libraries.messagebar
  compile parent.libraries.schematic
  apt parent.libraries.schematicCompiler

  compile parent.libraries.gson

  compile parent.libraries.appcompat
  compile parent.libraries.cardView
  compile parent.libraries.recyclerView
  compile parent.libraries.androidDesign

  compile parent.libraries.otto
  compile parent.libraries.dagger
  apt parent.libraries.daggerCompiler
  compile parent.libraries.picasso
  compile parent.libraries.retrofit
  compile parent.libraries.okhttp

  compile parent.libraries.butterknife
  compile parent.libraries.timber

  compile parent.libraries.dashclockApi

  compile(parent.libraries.crashlytics) {
    transitive = true
  }

  testCompile parent.libraries.junit
  testCompile parent.libraries.robolectric
  testCompile parent.libraries.mockito
  testCompile parent.libraries.assertj
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    minSdkVersion parent.ext.minSdkVersion
    targetSdkVersion parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName

    buildConfigField "String", "BUILD_TIME", "\"" + buildTime() + "\""

    buildConfigField "String", "TRAKT_CLIENT_ID", "\"" + getTraktClientId() + "\""
    buildConfigField "String", "TRAKT_SECRET", "\"" + getTraktSecret() + "\""
    buildConfigField "String", "TRAKT_REDIRECT_URL", "\"" + getTraktRedirectUrl() + "\""
  }

  signingConfigs {
    release
  }

  buildTypes {
    debug {
      buildConfigField "String", "PROVIDER_AUTHORITY",
          "\"net.simonvt.cathode.debug.provider.CathodeProvider\""
      resValue "string", "authority_provider", "net.simonvt.cathode.debug.provider.CathodeProvider"

      buildConfigField "String", "AUTHORITY_DUMMY_CALENDAR",
          "\"net.simonvt.cathode.debug.provider.DummyCalendarProvider\""
      resValue "string", "authority_dummy_calendar",
          "net.simonvt.cathode.debug.provider.DummyCalendarProvider"

      applicationIdSuffix ".debug"

      ext.enableCrashlytics = false
    }
    release {
      buildConfigField "String", "PROVIDER_AUTHORITY",
          "\"net.simonvt.cathode.provider.CathodeProvider\""
      resValue "string", "authority_provider", "net.simonvt.cathode.provider.CathodeProvider"

      buildConfigField "String", "AUTHORITY_DUMMY_CALENDAR",
          "\"net.simonvt.cathode.debug.provider.DummyCalendarProvider\""
      resValue "string", "authority_dummy_calendar",
          "net.simonvt.cathode.provider.DummyCalendarProvider"

      signingConfig signingConfigs.release

      ext.enableCrashlytics = !isOnCI()
    }
  }

  dexOptions {
    preDexLibraries = !isOnCI()
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  lintOptions {
    abortOnError false
  }
}

def isOnCI() {
  def ci = System.getenv('CI')
  return ci != null && ci == 'travis'
}

def buildTime() {
  def date = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  date.setTimeZone(TimeZone.getTimeZone("UTC"))
  return date.format(new Date())
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.applicationVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  project.tasks.getByName("check").dependsOn checkstyle
}

def getTraktClientId() {
  def clientId = getProperty('trakt', 'trakt.clientId')

  if (!clientId) {
    clientId = '8da90b18271eff4311d02c6b3c51f4925127c140fccf7f15bdbfe82a6f628c03'
  }

  return clientId
}

def getTraktSecret() {
  def secret = getProperty('trakt', 'trakt.secret')

  if (!secret) {
    secret = 'a6d4e2366529691707d97cbd42e4a2344b4141d1b7354e1f93900b0686961680'
  }

  return secret
}

def getTraktRedirectUrl() {
  def redirectUrl = getProperty('trakt', 'trakt.redirectUrl')

  if (!redirectUrl) {
    redirectUrl = 'cathode://oauth/authorize'
  }

  return redirectUrl
}

if (project.hasProperty('cathodeStoreFile')) {
  android.signingConfigs.release.storeFile = file(cathodeStoreFile)
}
if (project.hasProperty('cathodeStorePassword')) {
  android.signingConfigs.release.storePassword = cathodeStorePassword
}
if (project.hasProperty('cathodeKeyPassword')) {
  android.signingConfigs.release.keyPassword = cathodeKeyPassword
}
if (project.hasProperty('cathodeKeyAlias')) {
  android.signingConfigs.release.keyAlias = cathodeKeyAlias
}

if (isOnCI()) {
  printf 'Running on Travis-CI'
  android.signingConfigs.release.storeFile = android.signingConfigs.debug.storeFile
  android.signingConfigs.release.storePassword = android.signingConfigs.debug.storePassword
  android.signingConfigs.release.keyPassword = android.signingConfigs.debug.keyPassword
  android.signingConfigs.release.keyAlias = android.signingConfigs.debug.keyAlias
}
