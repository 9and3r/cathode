import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'

dependencies {
  compile project(':trakt-api')
  compile project(':libs:colorpicker')

  compile deps.schematic
  annotationProcessor deps.schematicCompiler

  compile deps.gson

  compile deps.appcompat
  compile deps.cardView
  compile deps.recyclerView
  compile deps.androidDesign

  compile deps.dagger
  annotationProcessor deps.daggerCompiler
  compile deps.picasso
  compile deps.picassoOkHttp
  compile deps.retrofit
  compile deps.okhttp
  debugCompile deps.okhttpLogger

  compile deps.butterknife
  annotationProcessor deps.butterknifeCompiler
  compile deps.timber

  compile deps.dashclockApi

  compile deps.tmdb

  compile(deps.crashlytics) {
    transitive = true
  }

  testCompile deps.junit
  testCompile deps.robolectric
  testCompile deps.truth
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    minSdkVersion parent.ext.minSdkVersion
    targetSdkVersion parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName

    vectorDrawables.useSupportLibrary = true

    buildConfigField "String", "TRAKT_CLIENT_ID", "\"" + getTraktClientId() + "\""
    buildConfigField "String", "TRAKT_SECRET", "\"" + getTraktSecret() + "\""
    buildConfigField "String", "TRAKT_REDIRECT_URL", "\"" + getTraktRedirectUrl() + "\""
    buildConfigField "String", "TMDB_API_KEY", "\"" + getTmdbApiKey() + "\""
  }

  signingConfigs {
    release
  }

  buildTypes {
    debug {
      buildConfigField "String", "BUILD_TIME", "\"2016-01-01T00:00Z\""

      buildConfigField "String", "PROVIDER_AUTHORITY",
          "\"net.simonvt.cathode.debug.provider.CathodeProvider\""
      resValue "string", "authority_provider", "net.simonvt.cathode.debug.provider.CathodeProvider"

      buildConfigField "String", "AUTHORITY_DUMMY_CALENDAR",
          "\"net.simonvt.cathode.debug.provider.DummyCalendarProvider\""
      resValue "string", "authority_dummy_calendar",
          "net.simonvt.cathode.debug.provider.DummyCalendarProvider"

      applicationIdSuffix ".debug"

      ext.enableCrashlytics = false
    }
    release {
      buildConfigField "String", "BUILD_TIME", "\"" + buildTime() + "\""

      buildConfigField "String", "PROVIDER_AUTHORITY",
          "\"net.simonvt.cathode.provider.CathodeProvider\""
      resValue "string", "authority_provider", "net.simonvt.cathode.provider.CathodeProvider"

      buildConfigField "String", "AUTHORITY_DUMMY_CALENDAR",
          "\"net.simonvt.cathode.provider.DummyCalendarProvider\""
      resValue "string", "authority_dummy_calendar",
          "net.simonvt.cathode.provider.DummyCalendarProvider"

      signingConfig signingConfigs.release

      ext.enableCrashlytics = !isOnCI()
    }
  }

  dexOptions {
    preDexLibraries = !isOnCI()
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat = 'full'
  }
}

def isOnCI() {
  def ci = System.getenv('CI')
  return ci != null && ci == 'travis'
}

def buildTime() {
  def date = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  date.setTimeZone(TimeZone.getTimeZone("UTC"))
  return date.format(new Date())
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.applicationVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  project.tasks.getByName("check").dependsOn checkstyle
}

def getTraktClientId() {
  def clientId = getProperty('api', 'trakt.clientId')

  if (!clientId) {
    clientId = '8da90b18271eff4311d02c6b3c51f4925127c140fccf7f15bdbfe82a6f628c03'
  }

  return clientId
}

def getTraktSecret() {
  def secret = getProperty('api', 'trakt.secret')

  if (!secret) {
    secret = 'a6d4e2366529691707d97cbd42e4a2344b4141d1b7354e1f93900b0686961680'
  }

  return secret
}

def getTraktRedirectUrl() {
  def redirectUrl = getProperty('api', 'trakt.redirectUrl')

  if (!redirectUrl) {
    redirectUrl = 'cathode://oauth/authorize'
  }

  return redirectUrl
}

def getTmdbApiKey() {
  def redirectUrl = getProperty('api', 'tmdb.apiKey')

  return redirectUrl
}

if (project.hasProperty('cathodeStoreFile')) {
  android.signingConfigs.release.storeFile = file(cathodeStoreFile)
}
if (project.hasProperty('cathodeStorePassword')) {
  android.signingConfigs.release.storePassword = cathodeStorePassword
}
if (project.hasProperty('cathodeKeyPassword')) {
  android.signingConfigs.release.keyPassword = cathodeKeyPassword
}
if (project.hasProperty('cathodeKeyAlias')) {
  android.signingConfigs.release.keyAlias = cathodeKeyAlias
}

if (isOnCI()) {
  printf 'Running on Travis-CI'
  android.signingConfigs.release.storeFile = android.signingConfigs.debug.storeFile
  android.signingConfigs.release.storePassword = android.signingConfigs.debug.storePassword
  android.signingConfigs.release.keyPassword = android.signingConfigs.debug.keyPassword
  android.signingConfigs.release.keyAlias = android.signingConfigs.debug.keyAlias
}

apply from: rootProject.file('deps.gradle')
