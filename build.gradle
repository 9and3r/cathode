buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.1.0'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    classpath 'io.fabric.tools:gradle:1.+'
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
  }
}

ext {
  enableCrashlytics = false
  versionMajor = 3
  versionMinor = 5
  versionPatch = 0

  versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch
  versionName = versionMajor + "." + versionMinor + "." + versionPatch

  minSdkVersion = 16
  targetSdkVersion = 23

  compileSdkVersion = 23
  buildToolsVersion = '23.0.3'
}

ext.libraries = [
  junit: 'junit:junit:4.12',
  robolectric: 'org.robolectric:robolectric:3.0',
  mockito: 'org.mockito:mockito-core:1.10.19',
  assertj: 'org.assertj:assertj-core:3.2.0',
  truth: 'com.google.truth:truth:0.27',

  schematic: 'net.simonvt.schematic:schematic:0.6.7',
  schematicCompiler: 'net.simonvt.schematic:schematic-compiler:0.6.7',

  gson: 'com.google.code.gson:gson:2.6.2',

  appcompat: 'com.android.support:appcompat-v7:23.3.0',
  cardView: 'com.android.support:cardview-v7:23.3.0',
  recyclerView: 'com.android.support:recyclerview-v7:23.3.0',
  androidDesign: 'com.android.support:design:23.3.0',

  dagger: 'com.squareup.dagger:dagger:1.2.2',
  daggerCompiler: 'com.squareup.dagger:dagger-compiler:1.2.2',
  okhttp: 'com.squareup.okhttp3:okhttp:3.2.0',
  otto: 'com.squareup:otto:1.3.8',
  picasso: 'com.squareup.picasso:picasso:2.5.2',
  retrofit: 'com.squareup.retrofit2:retrofit:2.0.2',
  retrofitGson: 'com.squareup.retrofit2:converter-gson:2.0.2',

  butterknife: 'com.jakewharton:butterknife:8.0.1',
  butterknifeCompiler: 'com.jakewharton:butterknife-compiler:8.0.1',
  timber: 'com.jakewharton.timber:timber:4.1.2',

  dashclockApi: 'com.google.android.apps.dashclock:dashclock-api:2.0.0',

  crashlytics: 'com.crashlytics.sdk.android:crashlytics:2.5.5@aar',

  jodaTime: 'joda-time:joda-time:2.7'
]

def getProperty(String propertyFile, String field) {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, propertyFile + ".properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
      properties.load(instr)
    }
    def value = properties.getProperty(field)
    return value
  }

  return ""
}
