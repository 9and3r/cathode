buildscript {
  repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:1.1.+'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
  }
}

ext {
  enableCrashlytics = false
  versionMajor = 2
  versionMinor = 1
  versionPatch = 1
  versionBuild = 1

  versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
  versionName = versionMajor + "." + versionMinor + "." + versionPatch

  minSdkVersion = 16
  targetSdkVersion = 21

  compileSdkVersion = 21
  buildToolsVersion = '21.1.2'
}

ext.libraries = [
  androidApt: 'com.neenbedankt.gradle.plugins:android-apt:1.4',

  junit: 'junit:junit:4.11',
  robolectric: 'org.robolectric:robolectric:2.3',
  mockito: 'org.mockito:mockito-core:1.10.8',
  assertj: 'org.assertj:assertj-core:1.7.0',


  messagebar: 'net.simonvt.messagebar:messagebar:1.0.2',
  schematic: 'net.simonvt.schematic:schematic:0.6.0',
  schematicCompiler: 'net.simonvt.schematic:schematic-compiler:0.6.0',

  gson: 'com.google.code.gson:gson:2.3.1',

  appcompat: 'com.android.support:appcompat-v7:21.0.3',
  cardView: 'com.android.support:cardview-v7:21.0.3',
  recyclerView: 'com.android.support:recyclerview-v7:21.0.3',

  dagger: 'com.squareup.dagger:dagger:1.2.2',
  daggerCompiler: 'com.squareup.dagger:dagger-compiler:1.2.2',
  okhttp: 'com.squareup.okhttp:okhttp:2.2.0',
  okhttpUrlConnection: 'com.squareup.okhttp:okhttp-urlconnection:2.2.0',
  otto: 'com.squareup:otto:1.3.5',
  picasso: 'com.squareup.picasso:picasso:2.4.0',
  retrofit: 'com.squareup.retrofit:retrofit:1.9.0',
  tape: 'com.squareup:tape:1.2.3',

  butterknife: 'com.jakewharton:butterknife:6.0.0',
  timber: 'com.jakewharton.timber:timber:2.5.0',

  dashclockApi: 'com.google.android.apps.dashclock:dashclock-api:2.0.0',

  crashlytics: 'com.crashlytics.android:crashlytics:1.+',

  jodaTime: 'joda-time:joda-time:2.6'
]

def getPropertiesField(String field) {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, "local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
      properties.load(instr)
    }
    def clientId = properties.getProperty(field)
    return clientId
  }

  return ""
}
