buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:1.2.3'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    classpath 'io.fabric.tools:gradle:1.+'
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
  }
}

ext {
  enableCrashlytics = false
  versionMajor = 3
  versionMinor = 0
  versionPatch = 4

  versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch
  versionName = versionMajor + "." + versionMinor + "." + versionPatch

  minSdkVersion = 16
  targetSdkVersion = 22

  compileSdkVersion = 22
  buildToolsVersion = '22.0.1'
}

ext.libraries = [
  androidApt: 'com.neenbedankt.gradle.plugins:android-apt:1.6',

  junit: 'junit:junit:4.11',
  robolectric: 'org.robolectric:robolectric:2.3',
  mockito: 'org.mockito:mockito-core:1.10.8',
  assertj: 'org.assertj:assertj-core:1.7.0',

  messagebar: 'net.simonvt.messagebar:messagebar:1.0.2',
  schematic: 'net.simonvt.schematic:schematic:0.6.2',
  schematicCompiler: 'net.simonvt.schematic:schematic-compiler:0.6.2',

  gson: 'com.google.code.gson:gson:2.3.1',

  appcompat: 'com.android.support:appcompat-v7:22.2.1',
  cardView: 'com.android.support:cardview-v7:22.2.1',
  recyclerView: 'com.android.support:recyclerview-v7:22.2.1',
  androidDesign: 'com.android.support:design:22.2.1',

  dagger: 'com.squareup.dagger:dagger:1.2.2',
  daggerCompiler: 'com.squareup.dagger:dagger-compiler:1.2.2',
  okhttp: 'com.squareup.okhttp:okhttp:2.4.0',
  otto: 'com.squareup:otto:1.3.8',
  picasso: 'com.squareup.picasso:picasso:2.5.2',
  retrofit: 'com.squareup.retrofit:retrofit:1.9.0',

  butterknife: 'com.jakewharton:butterknife:7.0.1',
  timber: 'com.jakewharton.timber:timber:2.7.1',

  dashclockApi: 'com.google.android.apps.dashclock:dashclock-api:2.0.0',

  crashlytics: 'com.crashlytics.sdk.android:crashlytics:2.5.1@aar',

  jodaTime: 'joda-time:joda-time:2.7'
]

def getPropertiesField(String field) {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, "local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
      properties.load(instr)
    }
    def clientId = properties.getProperty(field)
    return clientId
  }

  return ""
}
