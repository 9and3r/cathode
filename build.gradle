buildscript {
  apply from: rootProject.file('deps.gradle')

  repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.3'
    classpath 'io.fabric.tools:gradle:1.+'
    classpath 'gradle.plugin.com.kageiit:lintrules:1.1.3'
    classpath deps.butterknifeGradle
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
  }
}

ext {
  enableCrashlytics = false
  versionMajor = 5
  versionMinor = 3
  versionPatch = 3

  versionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
  versionName = versionMajor + "." + versionMinor + "." + versionPatch

  minSdkVersion = 16
  targetSdkVersion = 25

  compileSdkVersion = 25
  buildToolsVersion = '25.0.2'
}

def getProperty(String propertyFile, String field) {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, propertyFile + ".properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr -> properties.load(instr)
    }
    def value = properties.getProperty(field)
    return value
  }

  return ""
}

apply from: rootProject.file('deps.gradle')
